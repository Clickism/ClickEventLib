plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'me.clickism'
version = plugin_version

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    mavenLocal()
}

configurations {
    includeLib
}

dependencies {
    compileOnly("org.spigotmc:spigot-api:1.20.1-R0.1-SNAPSHOT")
    api("org.jetbrains:annotations:24.0.1")
    includeLib(api("me.clickism:SubcommandAPI:1.1"))
    includeLib(api("me.clickism:ClickGUI:1.0"))
}

jar {
    from {
        configurations.includeLib.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

tasks.register('sourcesJar', Jar) {
    from sourceSets.main.allSource
    archiveClassifier.set('sources')
}

tasks.register('javadocJar', Jar) {
    from tasks.javadoc
    archiveClassifier.set('javadoc')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact tasks.sourcesJar
            artifact tasks.javadocJar
        }
    }
    repositories {
        maven {
            name = "mavenLocal" // Or your remote repository
        }
    }
}

tasks.named('publish') {
    dependsOn tasks.sourcesJar, tasks.javadocJar
}